# -*- encoding:utf-8 Mode: POLY-ORG;  -*- ---
#+Startup: noindent
#+PROPERTY: literate-lang lisp
#+PROPERTY: literate-load yes

[[http://quickdocs.org/literate-lisp/][file:http://quickdocs.org/badge/literate-lisp.svg]]
[[https://github.com/jingtaozf/literate-lisp/actions][file:https://github.com/jingtaozf/literate-lisp/workflows/Continous%20Integration/badge.svg]]

* Table of Contents                                            :TOC:noexport:
- [[#introduction][Introduction]]
- [[#tutorial][Tutorial]]
  - [[#install-polymode-in-emacs][install polymode in Emacs]]
  - [[#how-to-insert-code-block-quickly][how to insert code block quickly]]
  - [[#a-new-code-block-header-argument-load][a new code block header argument ~load~]]
  - [[#web-syntax][WEB syntax]]
  - [[#how-to-debug-org-file-in-lispworks-ide][How to debug org file in LispWorks IDE]]
  - [[#how-to-write-user-initialization-file-with-literate-programming-style][How to write user initialization file with literate programming style]]
  - [[#a-demo-lisp-package][A demo lisp package]]
    - [[#prepare-the-asd-definition-file][Prepare the ASD definition file]]
    - [[#package-definition][package definition]]
    - [[#a-demo-function-to-a-recursive-assoc-implementation][a demo function to a recursive assoc implementation]]
  - [[#test-cases-for-this-demo-project][Test cases for this demo project]]
    - [[#preparation][Preparation]]
    - [[#tests-for-recursive-assoc][tests for ~recursive-assoc~]]
    - [[#run-all-tests-in-this-library][run all tests in this library]]

* Introduction
[[https://github.com/jingtaozf/literate-lisp][literate-lisp]] provides an easy way to use [[http://www.literateprogramming.com/][literal programming]] in Common Lisp language.
It extends the Common Lisp [[https://www.cs.cmu.edu/Groups/AI/html/cltl/clm/node187.html][reader syntax]]
so a Common Lisp vendor can read org files as Common Lisp source files.

By using this package([[https://github.com/jingtaozf/literate-lisp][literate-lisp]]), Emacs [[https://orgmode.org/][org mode]] and elisp library [[https://polymode.github.io/][polymode]],
literate programming can be very easy in one org file containing both documentation and source codes,
and this org file can interact well with [[https://common-lisp.net/project/slime/][SLIME]].

The implementation detail of [[https://github.com/jingtaozf/literate-lisp][literate-lisp]] is in file [[./literate-lisp.org]] ([[./literate-lisp.pdf][pdf version]]).

This library contains the following files:
- [[./literate-lisp.org]] \\
  The implementation and documentation of literate lisp reader.
- [[./literate-lisp.lisp]] \\
  The tangled codes of literate lisp reader, generated from [[./literate-lisp.org]]
- [[./literate-lisp.pdf]] \\
  The weaved documentation, generated from [[./literate-lisp.org]] by org mode's [[https://orgmode.org/manual/Triggering-publication.html#Triggering-publication][publish feature]].
- [[./readme.org]] \\
  This file contains introduction and demo codes for how to do literate lisp in an org file.
- [[./.github/workflows/continuous-integration.yml][continuous-integration.yml]] \\
  The config file used by Web service [[https://github.com/jingtaozf/literate-lisp/actions][github actions]] to test this library.
- [[./literate-lisp.asd]] \\
  The ASDF definition for literate-lisp project.
- [[./literate-demo.asd]] \\
  The ASDF definition for literate demo project.

* Tutorial
** install polymode in Emacs
It's better to edit the org file with [[https://polymode.github.io/][polymode]],which will make code block use its native file mode.
The following elisp scripts in .emacs will install it.
#+BEGIN_SRC elisp
(use-package poly-org
    :ensure t)
#+END_SRC
** how to insert code block quickly
Please have a look of the section [[https://github.com/jingtaozf/literate-elisp/blob/master/literate-elisp.org#how-to-insert-code-block-in-org-file][How to insert code block in org file]] in library [[https://github.com/jingtaozf/literate-elisp][literate-elisp]].
** a new code block header argument ~load~
Please have a look of the section [[./literate-lisp.org#new-defined-header-argument-load][new defined header argument load]] in [[./literate-lisp.org]].
** WEB syntax
There is only a [[./literate-lisp.org#WEB-Specification][simple specification]] and a [[./literate-lisp.org#test-for-web-syntax][simple case]] for it.

~This part is still in the experimental stage~.

If you want to highlight WEB syntax in Emacs lisp mode, please add the following code into your Emacs configuration file:
#+BEGIN_SRC elisp :load no
(font-lock-add-keywords
 'lisp-mode
 `(("\\(defun-literate\\|@=\\|:@@\\||:@\\)"
    . font-lock-keyword-face)))
#+END_SRC

** How to debug org file in LispWorks IDE
You have to add the following codes in your ~.lispworks~ to enable the debug facility in Lispworks Editor.
#+BEGIN_SRC lisp :load no
(defun check-org-mode (buffer truename)
  (when (and truename (equal (pathname-type truename) "org"))
    (setf (editor:buffer-major-mode buffer) "Lisp")))
(editor:add-global-hook editor::read-file-hook 'check-org-mode)
#+END_SRC
Thanks for Martin Simmons in [[http://www.lispworks.com/][LispWorks]] to support the above configuration codes.
** How to write user initialization file with literate programming style
You can put all initialization codes in an org source file, all you need is to load ~literate-lisp~ firstly.
For example, you can put the following codes in file [[http://www.sbcl.org/manual/#Initialization-Files][~$HOME/.sbclrc~]] for SBCL.
#+BEGIN_SRC lisp :load no
(require :asdf)
(load "~/projects/common-lisp/literate-lisp/literate-lisp.asd")
(asdf:oos 'asdf:load-op :literate-lisp)
(let ((*readtable* literate-lisp::*org-readtable*))
  (load "~/projects/common-lisp/config/init-lisp.org"))
#+END_SRC
I find it useful for various Lisp vendors so all initialization codes for them can be in just one file.

** A demo lisp package
*** Prepare the ASD definition file

We use the original ASD definition file, and extend the ASDF syntax(The documentation of extended ASDF syntax can be found in [[https://github.com/jingtaozf/literate-lisp/blob/master/literate-lsp.org#make-asdf-handle-org-file-correctly][literate-lisp.org]]).

In a short word,we should load ~literate-lisp~ by ASDF keyword ~:defsystem-depends-on~ and
declare the org source file with new ASDF keyword ~:org~.

Now let's define the ASDF system file [[./literate-demo.asd]] for this demo package
#+BEGIN_SRC elisp :load no
(asdf:defsystem literate-demo
  :author "Xu Jingtao <jingtaozf@gmail.com>"
  :version "0.1"
  :licence "MIT"
  :serial t
  :description "an demo project of literate-lisp"
  :defsystem-depends-on ("literate-lisp")
  :components ((:module :demo :pathname "./"
                        :components ((:org "readme"))))
  :properties ((version "0.1")))
#+END_SRC
Which will load this [[./readme.org]] directly as a lisp source file.

The whole content of ASDF definition file is in [[./literate-demo.asd]].

*** package definition

Firstly we define a new package for this demo project and switch to it.
#+BEGIN_SRC lisp
(in-package :common-lisp-user)
(defpackage :literate-demo (:use :cl))
(in-package :literate-demo)
#+END_SRC
*** a demo function to a recursive assoc implementation
As a demo project, we just show a demo function ~recursive-assoc~ here.

The function [[http://clhs.lisp.se/Body/f_assocc.htm][assoc]] only find first level item from an [[http://clhs.lisp.se/Body/26_glo_a.htm#association_list][association list]]. When I want to
find item from an association list contains a lot of other association list with very deep level, I
have to write codes like this which is too long.
#+BEGIN_SRC lisp :load test
(cdr (assoc :b (cdr (assoc :a '((:a . ((:b . c))))))))
#+END_SRC
To simplify the expression, a macro ~recursive-assoc~ to simply to the following form provides:
#+BEGIN_SRC lisp :load no
(recursive-assoc '((:a . ((:b . c)))) :a :b)
#+END_SRC

A macro function to expand lisp expressions recursively provides firstly.
#+BEGIN_SRC lisp
(eval-when (:compile-toplevel :load-toplevel :execute)
  (defun expand-recursive-assoc (alist recursive-properties)
    (if (cdr recursive-properties)
      (expand-recursive-assoc `(cdr (assoc ,(car recursive-properties) ,alist))
                              (cdr recursive-properties))
      `(cdr (assoc ,(car recursive-properties) ,alist)))))
#+END_SRC

Then the implementation macro is simple(test is here: ref:test-recursive-assoc)
#+BEGIN_SRC lisp
(defmacro recursive-assoc (alist &rest recursive-properties)
  (when recursive-properties
    (expand-recursive-assoc alist recursive-properties)))
#+END_SRC

For example, you can use it like this,which get ~:b~ part of the sub association list
which is ~:a~ part of the original list.
#+BEGIN_SRC lisp :load test
(equal 'c (recursive-assoc '((:a . ((:b . c)))) :a :b))
#+END_SRC
** Test cases for this demo project
*** Preparation
Now it's time to validate some functions.
The [[https://common-lisp.net/project/fiveam/][FiveAM]] library is used to test.
#+BEGIN_SRC lisp :load test
(eval-when (:compile-toplevel :load-toplevel :execute)
  (unless (find-package :fiveam)
    #+quicklisp (ql:quickload :fiveam)
    #-quicklisp (asdf:load-system :fiveam)))
(5am:def-suite literate-demo-suite :description "The test suite of literate-demo.")
(5am:in-suite literate-demo-suite)
#+END_SRC
*** tests for ~recursive-assoc~
label:test-recursive-assoc
#+BEGIN_SRC lisp :load test
(5am:test recursive-assoc
  (5am:is (equal 'c (recursive-assoc '((:a . ((:b . c)))) :a :b))))
#+END_SRC
*** run all tests in this library
This function is the entry point to run all tests and return true if all test cases pass.
#+BEGIN_SRC lisp :load test
(defun run-test ()
  (5am:run! 'literate-demo-suite))
#+END_SRC
Web service [[https://github.com/jingtaozf/literate-lisp/actions][github actions]] will load config file [[./.github/workflows/continuous-integration.yml][continuous-integration.yml]] to run these tests automatically
every time there is a new git change.
